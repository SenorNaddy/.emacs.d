#+STARTUP: content
#+OPTIONS: toc:4 h:4
* Configuration
This document is the result of me wanting to test literate programming
and thinking that my emacs configuration was the perfect candidate. It
is directly inspired by [[http://sachachua.com/blog/2012/06/literate-programming-emacs-configuration-file/][a blog post]] by Sacha Chua and others.

** General configuration
*** Encoding
I had some trouble with getting emacs to set the correct encoding in
OS X at one point. It seemed to go away if I called these functions:
#+begin_src emacs-lisp
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(prefer-coding-system 'utf-8)
#+end_src
I do not know if I still need these, but they stay as it isn't a
problem that they're there.

*** Defaults
The default is already set so it needs to be setq'ed instead. The
reason for setting this is that otherwise emacs starts at =/=.
#+begin_src emacs-lisp
  (setq default-directory "~/")
#+end_src

Set the file used for the auto-custom configuration to custom.el.
#+begin_src emacs-lisp
  (setq custom-file (expand-file-name "custom.el" user-emacs-directory))
  (load custom-file)
#+end_src

#+begin_src emacs-lisp
(setq-default
 backup-directory-alist `(("." . ,(concat user-emacs-directory "backups")))
 color-theme-is-global t
 indent-tabs-mode nil
 inhibit-startup-message t
 inhibit-startup-screen t
 message-log-max t
 mouse-wheel-follow-mouse t ;; scroll window under mouse
 mouse-wheel-progressive-speed nil ;; don't accelerate scrolling
 mouse-wheel-scroll-amount '(1 ((shift) . 1)) ;; one line at a time
 ns-pop-up-frames nil
 require-final-newline t
 ring-bell-function #'ignore
 use-dialog-box nil
 visible-bell nil
 )
#+end_src

#+begin_src emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)
#+end_src

#+begin_src emacs-lisp
(put 'narrow-to-region 'disabled nil)
(put 'narrow-to-page 'disabled nil)
#+end_src
*** Packaging
I use the package.el packaging system for emacs.
#+begin_src emacs-lisp
  (require 'package)
#+end_src

Usually we should be running on an emacs that is > 24, but on the off
chance that we are on a new system that has an older version we should
add the gnu packages.
#+begin_src emacs-lisp
  (when (< emacs-major-version 24)
    (add-to-list 'package-archives '("gnu" . "http://elpa.gnu.org/packages/")))
#+end_src

I prefer to get both the marmalade and melpa packages. I like the
bleeding edge, but I also keep marmalade for the times that melpa does
not have that package.
#+begin_src emacs-lisp
  (add-to-list 'package-archives '("marmalade" . "http://marmalade-repo.org/packages/") t)
  (add-to-list 'package-archives '("melpa" . "http://melpa.milkbox.net/packages/") t)
#+end_src

I am not sure if this is needed. Especially with the use-package
library installed. I think I should figure out a way to use that
better.
#+begin_src emacs-lisp
  (package-initialize)
#+end_src

If this is the first time we are loading or the elpa folder has been
deleted we need to refresh the archive list.
#+begin_src emacs-lisp
  (when (not package-archive-contents)
    (package-refresh-contents))
#+end_src

These are the packages that I install with package.el.
#+begin_src
  (defvar my-packages
    '(
      ace-jump-mode
      arduino-mode
      auctex
      auto-complete
      browse-kill-ring
      cider
      cljdoc
      clojure-mode
      clojure-test-mode
      color-theme
      color-theme-sanityinc-tomorrow
      command-frequency
      dired-single
      elisp-slime-nav
      expand-region
      find-file-in-project
      go-mode
      ibuffer-vc
      idle-highlight-mode
      ido-ubiquitous
      magit
      midje-mode
      org
      paredit
      pomodoro
      popwin
      prolog
      python-mode
      python-pep8
      python-pylint
      slime
      slime-repl
      smex
      use-package
      undo-tree
      visual-regexp
      ;; yasnippet
      )
    "A list of packages to ensure are installed at launch.")
#+end_src

Loop through all of my packages and install them if they are missing.
TODO: I would like to incorporate a minimal version check here as well.
#+begin_src
  (dolist (p my-packages)
    (when (not (package-installed-p p))
      (package-install p)))
#+end_src

*** use-package
Need to require use-package as soon as possible as it is needed for
the rest of the configurations.
#+begin_src emacs-lisp
  (require 'use-package)
#+end_src
I should look into =:ensure= as I can remove most of the package.el
code with it.
*** Interface
**** Font
I like the menlo font, so if I am running OS X we should try to load
that font.

#+begin_src emacs-lisp
  (if (and (window-system)
           (equal 'darwin system-type))
      (set-face-attribute 'default nil :font "menlo-14")
    (set-face-attribute 'default nil :font (font-get-system-font)))
#+end_src

**** Fringe
#+begin_src emacs-lisp
(set-fringe-mode '(0 . 6))
#+end_src
**** Color-theme

I like the tomorrow-night color theme.
#+begin_src emacs-lisp
  (require 'color-theme-sanityinc-tomorrow)
#+end_src

**** Frame size
#+begin_src emacs-lisp
;;; Found at http://stackoverflow.com/a/94277
(defun set-frame-size-according-to-resolution ()
  (interactive)
  (if window-system
  (progn
    (add-to-list 'default-frame-alist
                 (cons 'width
                       (if (> (x-display-pixel-width) 1280)
                           100 80)))
    (add-to-list 'default-frame-alist
         (cons 'height (/ (- (x-display-pixel-height) 50)
                          (frame-char-height)))))))

(defun x-maximize-frame ()
    (interactive)
    (x-send-client-message nil 0 nil "_NET_WM_STATE" 32
                           '(2 "_NET_WM_STATE_MAXIMIZED_VERT" 0))
    (x-send-client-message nil 0 nil "_NET_WM_STATE" 32
                           '(2 "_NET_WM_STATE_MAXIMIZED_HORZ" 0)))

(if (eq 'x window-system)
    (x-maximize-frame)
  (set-frame-size-according-to-resolution))

(set-frame-position (next-frame) 0 0)
#+end_src

*** OS X
The menu bar is annoying on systems other than OS X.
#+begin_src emacs-lisp
  (if (equal system-type 'darwin)
      (menu-bar-mode +1)
    (menu-bar-mode -1))
#+end_src

*** Define keys
#+begin_src emacs-lisp
(global-set-key (kbd "s--") 'text-scale-decrease)
(global-set-key (kbd "s-=") 'text-scale-increase)
(global-set-key (kbd "C-s") 'isearch-forward-regexp)
(global-set-key (kbd "C-r") 'isearch-backward-regexp)
(define-key 'help-command "a" 'apropos)

(global-set-key (kbd "s-[") 'backward-paragraph)
(global-set-key (kbd "s-]") 'forward-paragraph)
(global-set-key  [C-s-268632091] 'backward-sexp)
(global-set-key  [C-s-268632093] 'forward-sexp)

(define-key read-expression-map (kbd "TAB") 'lisp-complete-symbol)
(define-key lisp-mode-shared-map (kbd "RET") 'reindent-then-newline-and-indent)
#+end_src
#+begin_src emacs-lisp
(add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src
*** Path
Sometimes emacs is not so good at finding the correct paths to
everything.
#+begin_src emacs-lisp
(defcustom exec-paths '("~/.lein/bin" "/usr/local/bin" "~/.local/bin" "/usr/texbin")
  "Directories to be added to exec-path"
  :type 'string)
#+end_src

Add all of the custom paths to the PATH variable.
#+begin_src emacs-lisp
(defun add-to-path (dir)
  "Adds a dir to PATH if dir exists."
  (when (file-exists-p dir)
    (progn (add-to-list 'exec-path dir)
           (setenv "PATH" (concat (getenv "PATH") (concat ":" dir))))))

(defun initialize-exec-path ()
  (interactive)
  (dolist (dir exec-paths)
    (add-to-path dir)))

(initialize-exec-path)
#+end_src
*** Defuns
**** Sort symbols
Sort-lines and the other sorts are quite nice, but it is even nicer if
I am able to also sort symbols that are in a list.
#+begin_src emacs-lisp
;; found at http://www.emacswiki.org/emacs/SortWords
(defun sort-symbols (reverse beg end)
  "Sort symbols in region alphabetically, in REVERSE if negative.
    See `sort-words'."
  (interactive "*P\nr")
  (sort-regexp-fields reverse "\\(\\sw\\|\\s_\\)+" "\\&" beg end))
#+end_src

**** Keys
Convenience function to get all keys in a hash table.
#+begin_src emacs-lisp
(defun keys (hashtable)
  "Return all keys in hashtable."
  (let (allkeys)
    (maphash (lambda (kk vv) (setq allkeys (cons kk allkeys))) hashtable)
    allkeys))
#+end_src
*** Mode line
At some point I didn't like the standard mode line and started to
experiment with how I wanted it to look. Because of too much time and
not enough knowledge about alternatives, this monster came to be.

I feel it gets very distracting when the mode line changes depending
on which window is selected. I don't really need the visual
conformation that I have changed window. The cursor does that for me.
#+begin_src emacs-lisp
(setq mode-line-in-non-selected-windows nil)
#+end_src

I needed a function that truncated from the start of the list instead
of at the end. This function takes a string, reverses it, does the
normal truncate and reverses it again. There is probably a better way
of doing this, but this was the quick and dirty one I figured out on
my own.
#+begin_src emacs-lisp
(defun truncate-string-to-length (str end-column &optional start-column padding ellipsis)
  "The same as truncate-string-to-width,
except it truncates from the start of the list"
  (concat
   (reverse
    (append (truncate-string-to-width
             (concat (reverse (append (format  str) nil)))
             end-column start-column padding ellipsis)
            nil))))
#+end_src

I like to have a box around the mode-line to visually seperate it from
the rest of the frame.
#+begin_src emacs-lisp
(set-face-attribute 'mode-line nil
  :box '(:line-width 1
         :color "gray25"))
#+end_src

This is a helper function to center a string in a set width.
#+begin_src emacs-lisp
(defun center-string-in-char (str len char)
  (store-substring (make-string len char)
                   (/ (- len (length str)) 2) str))
#+end_src

This is the format for the buffer position numbers in the mode-line.
#+begin_src emacs-lisp
(setq-default mode-line-position '(" %03l:%2c"))
#+end_src

If I want to use the pomodoro-mode-line string in the mode-line I need
to give it a default of "" as otherwise we will get errors in the
message log.
#+begin_src emacs-lisp
(setq-default pomodoro-mode-line-string "")
#+end_src

Here we are setting the mode line format. It has a lot of
configurations. I should get around to commenting it at some point.
#+begin_src emacs-lisp
(setq-default mode-line-format
  '("%e "
    (:eval (if buffer-file-name "%* " "無常"))        ; file status
    (:eval
     (propertize
      (if (buffer-narrowed-p)
          " 狭"
        "")))

    mode-line-position
    "  "
    (:eval
     (propertize                        ; file/buffer name
      (center-string-in-char
       (truncate-string-to-length
        (or buffer-file-truename
            (buffer-name))
        25 nil nil  "..")
       25 ?\s)
      'help-echo (buffer-file-name)     ; echo full name
      'local-map
      (let ((map (make-sparse-keymap)))
        (define-key map [mode-line mouse-3]
          'mode-line-next-buffer)
        (define-key map [mode-line mouse-1]
          'mode-line-previous-buffer)
        map)))

    "  "

    (:eval
     (propertize mode-name
                 'help-echo (format-mode-line minor-mode-alist)))
    " "
    vc-mode
    "  "

    pomodoro-mode-line-string

    (:eval
     (concat
      (propertize " " 'display
                 `((space :align-to
                           (- right ,(if (string= "" pomodoro-mode-line-string) 20 8)))))
      (propertize (if (string= "" pomodoro-mode-line-string)
                      (format-time-string " %a %b %d, %H:%M")
                    (format-time-string " %H:%M"))                 ; time
                  'help-echo
                  (format-time-string "%A, %B %d, %Y, %H:%M"))))))
#+end_src
*** Minibuffer
A small configuration of the minibuffer to conditionally enable
paredit mode for when I am evaluation an expression rather then
calling an interactive command.
#+begin_src emacs-lisp
(defun conditionally-enable-paredit-mode ()
  "enable paredit-mode during eval-expression"
  (if (eq this-command 'eval-expression)
      (paredit-mode 1)))

(add-hook 'minibuffer-setup-hook 'conditionally-enable-paredit-mode)
#+end_src

** Minor modes
*** Global minor modes
These are just some global minor modes that I either want on or off.
#+begin_src emacs-lisp
(blink-cursor-mode -1)
(fringe-mode -1)
(global-hl-line-mode +1)
(global-linum-mode +1)
(scroll-bar-mode -1)
(show-paren-mode +1)
(tool-bar-mode -1)
(winner-mode +1)
#+end_src
**** Ace jump
#+begin_src emacs-lisp
(use-package ace-jump-mode
  :bind ("C-x SPC" . ace-jump-mode))
#+end_src

**** Yasnippet
#+begin_src emacs-lisp
(use-package yasnippet
  :commands (yas-global-mode yas-activate-extra-mode)
  :load-path "site-lisp/yasnippet"
  :init (yas-global-mode +1))
#+end_src

*** Expand-region
#+begin_src emacs-lisp
(use-package expand-region
  :bind (("C-=" . er/expand-region)
         ("C--" . er/contract-region)))
#+end_src
*** Auto complete
#+begin_src emacs-lisp
   (use-package auto-complete-config
  :init (ac-config-default)
  :config
  (progn
    (setq ac-auto-show-menu 0.3)
    (setq ac-use-menu-map t)
    (ac-config-default)
    (setq ac-sources
          (cons ac-source-yasnippet
                ac-sources))
    (define-key ac-complete-mode-map "\r" 'ac-expand)
    (define-key ac-complete-mode-map [return] 'ac-expand)
    (define-key ac-complete-mode-map "\t" 'ac-complete)
    (define-key ac-complete-mode-map [tab] 'ac-complete)
    (global-auto-complete-mode)))
#+end_src

*** Browse kill ring
#+begin_src emacs-lisp
(use-package browse-kill-ring
  :init (browse-kill-ring-default-keybindings))
#+end_src

*** Paredit
#+begin_src emacs-lisp
(use-package paredit
  :commands (enable-paredit-mode paredit-mode)
  :config
  (progn
    (defun paredit-delete-indentation ()
      (interactive)
      (delete-indentation)
      (prog-indent-sexp))

    (define-key paredit-mode-map (kbd "M-(") 'paredit-wrap-round)
    (define-key paredit-mode-map (kbd "M-)") 'paredit-close-round-and-newline)
    (define-key paredit-mode-map (kbd "M-[") 'paredit-wrap-square)
    (define-key paredit-mode-map (kbd "M-{") 'paredit-wrap-curly)
    (define-key paredit-mode-map (kbd "M-}") 'paredit-close-curly-and-newline)
    (define-key paredit-mode-map (kbd "M-j") 'paredit-delete-indentation)))
#+end_src

*** Command frequency
#+begin_src emacs-lisp
(use-package command-frequency
  :init (command-frequency-mode +1))
#+end_src
*** Dired
#+begin_src emacs-lisp
(use-package dired
  :init
  (if  (not (boundp 'dired-mode-map))
      (add-hook 'dired-load-hook
                (lambda ()
                  (define-key dired-mode-map [return]
                    'dired-single-buffer)
                  (define-key dired-mode-map [mouse-1]
                    'dired-single-buffer-mouse)
                  (define-key dired-mode-map "^"
                    (function
                     (lambda ()
                       (interactive)
                       (dired-single-buffer "..")))))))
  :config
  (use-package dired-single))
#+end_src

*** ibuffer
#+begin_src emacs-lisp
(use-package ibuffer
  :bind ("C-x C-b" . ibuffer)
  :config
  (progn
    (use-package ibuffer-vc
      :commands ibuffer-vc-set-filter-groups-by-vc-root)
    (use-package ibuffer-ext
      :commands ibuffer-do-sort-by-major-mode)

    (defvar ibuffer-initialized nil)
    (defun my-ibuffer-hook ()
      (unless ibuffer-initialized
        (ibuffer-vc-set-filter-groups-by-vc-root)

        (unless (eq ibuffer-sorting-mode 'major-mode)
          (ibuffer-do-sort-by-major-mode))

        (setq ibuffer-formats
              '((mark modified read-only vc-status-mini " "
                      (name 25 25 :left :elide)
                      " "
                      (size 9 -1 :right)
                      " "
                      (mode 16 16 :left :elide)
                      " "
                      (vc-status 16 16 :left)
                      " "
                      filename-and-process)))
        (setq ibuffer-expert t)
        (setq ibuffer-initialized t)))
    (add-hook 'ibuffer-hook 'my-ibuffer-hook)))
#+end_src
*** Find file at point
#+begin_src emacs-lisp
(use-package ffap
  :init (ffap-bindings))
#+end_src

*** ido
#+begin_src emacs-lisp
(use-package ido
  :init (ido-mode +1)
  :config
  (progn
    (use-package flx-ido
      :commands flx-ido-mode)
    (use-package ido-vertical-mode
      :commands ido-vertical-mode)
    (flx-ido-mode +1)
    (ido-vertical-mode +1)
    (setq id-use-faces nil
          ido-auto-merge-work-directories-length nil
          ido-create-new-buffer 'always
          ido-enable-flex-matching t
          ido-enable-prefix nil
          ido-handle-duplicate-virtual-buffers 2
          ido-max-prospects 10
          ido-use-filename-at-point 'guess
          ido-use-virtual-buffers t)))
#+end_src
*** ispell
#+begin_src emacs-lisp
(use-package ispell
  :defer t
  :config
  (setq-default ispell-program-name "/usr/local/bin/aspell"))
#+end_src
*** linum
#+begin_src emacs-lisp
(use-package linum
  :init (global-linum-mode +1)
  :config
  (progn
    (defvar my-linum-format-string "%4d")

    (add-hook 'linum-before-numbering-hook 'my-linum-get-format-string)

    (defun my-linum-get-format-string ()
      (let* ((width (length (number-to-string
                             (count-lines (point-min) (point-max)))))
             (format (concat "%" (number-to-string width) "d ")))
        (setq my-linum-format-string format)))

    (defun my-linum-format (line-number)
      (propertize (format my-linum-format-string line-number) 'face 'linum))

    (setq linum-format 'my-linum-format)))
#+end_src
*** popwin
#+begin_src emacs-lisp
(use-package popwin
  :commands popwin:display-buffer
  :init (setq display-buffer-function 'popwin:display-buffer))
#+end_src

*** Multiple cursors
#+begin_src emacs-lisp
(use-package multiple-cursors
  :bind (("C->" . mc/mark-next-like-this)
         ("C-<" . mc/mark-previous-like-this)
         ("C-c C-<" . mc/mark-all-like-this)))
#+end_src
*** pomodoro mode
#+begin_src emacs-lisp
(use-package pomodoro
  :commands pomodoro-start
  :bind (("C-c p s" . pomodoro-start)
         ("C-c p x" . pomodoro-stop))
  :config
  (progn
    (setq pomodoro-break-start-sound "~/Music/smw_pause.wav"
          pomodoro-work-start-sound "~/Music/smw_pause.wav"
          pomodoro-work-start-message "Back to work!"
          pomodoro-work-cycle "労働" ;; work in japanese
          pomodoro-break-cycle "休止" ;; break in japanese
          pomodoro-long-break-time 20
          pomodoro-break-time 7)))
#+end_src
*** Saveplace
#+begin_src emacs-lisp
(use-package saveplace
  :init
  (setq-default save-place-file (concat user-emacs-directory "places")
                save-place t))
#+end_src
*** smex
#+begin_src emacs-lisp
(use-package smex
  :init (smex-initialize)
  :bind ("M-x" . smex)
  :config
  (progn
    (setq smex-save-file (concat user-emacs-directory ".smex-items"))))
#+end_src

*** undo-tree
#+begin_src emacs-lisp
(use-package undo-tree
  :init (global-undo-tree-mode))
#+end_src

*** visual-regexp
#+begin_src emacs-lisp
(use-package visual-regexp
  :commands (vr/replace vr/query-replace)
  :bind (("C-c r" . vr/replace)
         ("C-c q" . vr/query-replace)))
#+end_src

** Major modes
*** Org
#+begin_src emacs-lisp
(use-package org
  :bind (("\C-cl" . org-store-link)
         ("\C-ca" . org-agenda)
         ("\C-cb" . org-iswitchb)
         ("\C-cc" . org-capture))
  :config
  (progn
    (defvar org-mode-initialized nil)
    (defun my-org-mode-hook ()
      (unless org-mode-initialized
        (setq org-directory "~/Dropbox/org"
              org-mobile-inbox-for-pull "~/Dropbox/org/inbox.org"
              org-mobile-directory "~/Dropbox/org/mobile"

              org-agenda-include-all-todo t
              org-agenda-files '("~/Dropbox/org/organizer.org")

              org-tag-persistent-alist '(("work" . ?w) ("private" . ?p))

              org-todo-keywords '((sequence "TODO" "STARTED" "WAITING"
                                            "|" "DONE" "CANCELLED" "ON-HOLD" "DEFERRED" "DELEGATED")
                                  (sequence "APPT" "|" "FINISHED" "CANCELLED" "MISSED")
                                  (sequence "BUG" "|" "FIXED")
                                  (sequence "NOTE"))

              org-todo-keyword-faces '(("STARTED" . "yellow")
                                       ("ON-HOLD" . "orange")
                                       ("CANCELLED" . "dim gray")
                                       ("NOTE" . "aqua"))

              org-refile-targets '(("organizer.org" :maxlevel . 9))
              org-completion-use-ido t
              org-latex-to-pdf-process '("texi2dvi --pdf --verbose --batch %f"))

       (unless (boundp 'org-export-latex-classes)
         (setq org-export-latex-classes nil))

       (add-to-list 'org-export-latex-classes
                    '("article"
                      "\\documentclass{article}
                \\usepackage[l2tabu, orthodox]{nag}
                \\usepackage{microtype}"
                      ("\\section{%s}" . "\\section*{%s}")
                      ("\\subsection{%s}" . "\\subsection*{%s}")
                      ("\\subsubsection{%s}" . "\\subsubsection*{%s}")))

       (add-to-list 'org-export-latex-classes
                    '("thesis"
                      "\\documentclass{report}
                \\usepackage[l2tabu, orthodox]{nag}
                \\usepackage{microtype}"
                      ("\\chapter{%s}" . "\\chapter*{%s}")
                      ("\\section{%s}" . "\\section*{%s}")
                      ("\\subsection{%s}" . "\\subsection*{%s}")
                      ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                      ("\\paragraph{%s}" . "\\paragraph*{%s}")
                      ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))

       (define-key org-mode-map (kbd "M-q") 'org-fill-paragraph)
       (visual-line-mode t)
       (setq fill-column 80)
       (setq ispell-parser 'tex)
       (font-lock-remove-keywords
        nil '(("\\<\\(FIX\\(ME\\)?\\|TODO\\|HACK\\|REFACTOR\\|NOCOMMIT\\)\\b"
               1 font-lock-warning-face t)))
       (add-to-list 'org-latex-packages-alist '("" "microtype"))
       (org-add-link-type
        "citet*" 'ebib
        (lambda (path desc format)
          (cond
           ((eq format 'latex)
            (if (or (not desc) (equal 0 (search "citet*:" desc)))
                (format "\\citet*{%s}" path)
              (format "\\citet*[%s]{%s}" desc path))))))


       ;;org-capture config
       (setq org-default-notes-file (concat org-directory "/organizer.org"))

       (setq org-capture-templates
             '(("a" "Appointments" entry
                (file+headline org-default-notes-file "Appointments")
                "* APPT %? %^{WITH}p %^{LOCATION}p\n%^T--%^T\n" :prepend)
               ("p" "Project" entry
                (file+headline org-default-notes-file "Projects")
                "* %?\n")
               ("d" "Done" entry
                (file+datetree (concat org-directory "/done.org"))
                "* %?\nCLOCK: %^U--%U")
               ("j" "Journal" entry
                (file+datetree (concat org-directory "/journal.org"))
                "* %?\nEntered on %U\n  %i\n  %a")
               ("n" "Note" entry
                (file+headline org-default-notes-file "Notes")
                "* NOTE %?\n")
               ("t" "Todo" entry
                (file+headline org-default-notes-file "Tasks")
                "* TODO %?\n  %i\n")))

       (defun org-export-latex-no-toc (depth)
         (when depth
           (format "%% Org-mode is exporting headings to %s levels.\n"
                   depth)))
       (setq org-export-latex-format-toc-function 'org-export-latex-no-toc)
       (setq org-mode-initialized t)))))
#+end_src emacs-lisp

*** Arduino mode
#+begin_src emacs-lisp
(use-package arduino-mode
  :mode ("\\.ino$" . arduino-mode)
  :config (add-hook 'arduino-mode-hook '(lambda ()
                                          (idle-highlight-mode +1))))
#+end_src

*** Clojure mode
#+begin_src emacs-lisp
(use-package cider
  :config
  (progn
    (add-hook 'cider-repl-mode-hook 'ac-nrepl-setup)
    (add-hook 'cider-repl-mode-hook 'enable-paredit-mode)
    (add-hook 'cider-repl-mode-hook 'subword-mode)
    (setq cider-repl-history-file "~/.emacs.d/history/nrepl")))

(use-package clojure-mode
  :mode (("\\.cljx?$" . clojure-mode)
         ("\\.dtm$" . clojure-mode)
         ("\\.edn$" . clojure-mode))
  :interpreter (("jark" . clojure-mode)
                ("cake" . clojure-mode))
  :config
  (progn
    (use-package cljdoc
      :config
      (progn
        (defadvice cljdoc-get-docstring (after truncate-docstring)
          (setq ad-return-value
                (truncate-string-to-width
                 (concat " " ad-return-value) (- (frame-width) 10) nil nil 't)))
        (ad-activate 'cljdoc-get-docstring)))
    (use-package clojure-jump-to-file)
    (use-package midje-mode
      :config (add-hook 'midje-mode-hook '(lambda ()
                                            (yas-activate-extra-mode 'midje-mode))))

    (defun clojure-jump-to-project-file ()
      (interactive)
      (let ((dir (file-name-as-directory
                  (locate-dominating-file buffer-file-name "src/"))))
        (find-file (concat dir "project.clj"))))

    (defvar clojure-mode-initialized nil)

    (defun my-clojure-mode-hook ()
      (unless clojure-mode-initialized
        (define-key clojure-mode-map  (kbd "C-x p") 'clojure-jump-to-project-file)

        (put-clojure-indent 'update-in 'defun)
        (put-clojure-indent 'get-in 'defun)
        (put-clojure-indent 'assoc-in 'defun)
        (put-clojure-indent 'assoc! 'defun)
        (put-clojure-indent 'swap! 'defun)
        (put-clojure-indent 'run* 'defun)
        (put-clojure-indent 'fresh 'defun)

        (setq clojure-mode-initialized t))
      (enable-paredit-mode))

    (add-hook 'clojure-mode-hook 'my-clojure-mode-hook)))

(define-derived-mode clojurescript-mode clojure-mode "ClojureScript"
  "Major mode for ClojureScript")

(use-package clojurescript-mode
  :mode ("\\.cljs$" . clojurescript-mode))
#+end_src

*** Emacs lisp
#+begin_src emacs-lisp
(use-package lisp-mode
  :config
  (progn
    (use-package elisp-slime-nav
      :commands elisp-slime-nav-mode)

    (add-hook 'emacs-lisp-mode-hook
              (lambda ()
                (make-local-variable 'after-save-hook)
                (add-hook 'after-save-hook
                          (lambda ()
                            (if (file-exists-p (concat buffer-file-name "c"))
                                (delete-file (concat buffer-file-name "c")))))))

    (add-hook 'emacs-lisp-mode-hook 'turn-on-eldoc-mode)
    (add-hook 'emacs-lisp-mode-hook 'elisp-slime-nav-mode)
    (add-hook 'emacs-lisp-mode-hook 'enable-paredit-mode)))
#+end_src

#+begin_src emacs-lisp
(use-package ielm
  :defer t
  :config
  (add-hook ielm-mode-hook 'enable-paredit-mode))
#+end_src

*** Go mode
#+begin_src emacs-lisp
(use-package go-mode
  :mode ("\\.go$" . go-mode))
#+end_src
*** Haskell mode
#+begin_src emacs-lisp
(use-package haskell-mode
  :mode (("\\.hs$" . haskell-mode)
         ("\\.lhs$" . literate-haskell-mode))
  :config
  (progn
    (add-hook 'haskell-mode-hook 'turn-on-haskell-indentation)
    (add-hook 'haskell-mode-hook 'turn-on-haskell-doc-mode)))
#+end_src

*** Magit
#+begin_src emacs-lisp
(use-package magit
  :bind ("C-x g" . magit-status))
#+end_src
*** prog-mode
#+begin_src emacs-lisp
(use-package prog-mode
  :config
  (add-hook 'prog-mode-hook (lambda () (idle-highlight-mode +1))))
#+end_src
*** Prolog
#+begin_src emacs-lisp
(use-package prolog-mode
  :commands (run-prolog prolog-mode mercury-mode)
  :mode (("\\.pl$" . prolog-mode)
         ("\\.m$" . mercury-mode))
  :config
  (setq prolog-system 'swi))
#+end_src
*** Python
#+begin_src emacs-lisp
(use-package python-mode
  :commands python-mode
  :mode ("\\.py$" . python-mode)
  :config
  (progn
    (use-package python-pep8)
    (use-package python-pylint)))
#+end_src

*** Common lisp
#+begin_src emacs-lisp
(setq inferior-lisp-program "sbcl")
(load-file (expand-file-name "~/quicklisp/slime-helper.el"))
(add-hook 'slime-repl-mode-hook 'enable-paredit-mode)
#+end_src

*** SPARQL
#+begin_src emacs-lisp
(use-package sparql-mode
  :load-path "site-lisp/sparql-mode"
  :mode ("\\.sparql$" . sparql-mode)
  :config
  (progn
    (add-to-list 'ac-dictionary-files "~/.emacs.d/site-lisp/sparql-mode/sparql-mode")
    (setq sparql-default-base-url "http://live.dbpedia.org/sparql")
    (add-hook 'sparql-result-mode-hook '(lambda () (linum-mode -1)))
    (add-hook 'sparql-result-mode-hook '(lambda () (toggle-truncate-lines 1)))))
#+end_src
